model PingPong {
		
	capsule Pinger {
		
		conjugate external port pingPort : PingPongProtocol;
		
		statemachine {
			initial start;
			state playing;

			transition t1 
				from start 
				to playing
				action {|#
						printf("Starting game\n");
						if ( pingPort.ping().send() ) {
							printf("ping sent!\n");
						} else {
							printf("Error sending Ping!\n");
						}	
				#|}
			;	
			
			transition t2
				from playing
				to playing
				triggers on pingPort : pong 
				action {|#
					printf("[Pinger] Pong received!\n");
					if (pingPort.ping().send()) {
						printf("[Pinger] Ping sent!\n");
					} else {
						printf("[Pinger] Error sending Ping!\n");
					}
				#|}
			;
		}
	}	
	
	capsule Ponger {
		external port pongPort : PingPongProtocol;

		statemachine {
			initial start;
			state playing ;
			
			transition t1 
				from start 
				to playing
				action {|##|}
			;
								
			transition t2
				from playing
				to playing
				triggers on pongPort : ping 
				action {|#
					printf("[Ponger] Ping received!\n");
					if (pongPort.pong().send()){
						printf("[Ponger] Pong sent!\n");
					} else {
						printf("[Ponger] Error sending Pong!\n");
					}
				#|}
			;
			
		}
	}

	capsule Top {
		part pinger : Pinger;
		part ponger : Ponger;
		
		connect pinger : pingPort to ponger : pongPort;
	}
	
	protocol PingPongProtocol {
		in message ping(); 
		out message pong();
	}

}